#UC3 CQ 2 	What ingredient role (excipient, ...) played by a substance <S> is part of what (authorized, investigational) medicinal product that has a therapeutic indication indicated by a MedDRA ID <I>?

PREFIX cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
PREFIX cmns-rlcmp: <https://www.omg.org/spec/Commons/RolesAndCompositions/>
PREFIX cmns-ctxdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
PREFIX cmns-dsg:   <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-id: <https://www.omg.org/spec/Commons/Identifiers/>
PREFIX cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>
PREFIX cmns-txt: <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX idmp-eura:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/EuropeanJurisdiction/EuropeanRegistrationAuthorities/>
PREFIX idmp-meddra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/MedicalDictionaryForRegulatoryActivities/>
PREFIX idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX idmp-phprd: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11616-PharmaceuticalProducts/> 
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:        <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?Substance ?SubstanceLabel ?IngredientRoleType ?ProductRole ?ProductRoleType
WHERE {

        # Bind Variable MedDRA ID <I>
        # Example "10020772" for $MedDraId
        BIND( uc3_cq2_parameter_1  AS $MedDRAId)

        # Find Medicinal Product that is approved for the TherapeuticIndication having MedDRA ID
		?TherapeuticIndication a idmp-mprd:TherapeuticIndication . 
		?TherapeuticIndication idmp-mprd:hasIndicationAsDiseaseSymptomProcedure ?MedicalConditionClassifier.
		?MedicalConditionClassifier a/rdfs:subClassOf* idmp-meddra:MedDRATerm;
		   (cmns-id:isIdentifiedBy|^cmns-id:identifies) ?MedDRAIdentifier .
		?MedDRAIdentifier cmns-txt:hasTextValue $MedDRAId .
	
	    # Find indications that are applicable in some product role
		{
			?TherapeuticIndication cmns-ctxdsg:isApplicableIn ?ProductRole .
			?ProductRole a idmp-mprd:AuthorizedMedicinalProduct .
		    ?ProductRole (cmns-rlcmp:isPlayedBy|^cmns-rlcmp:playsRole) ?MedicinalProduct  .
			idmp-mprd:AuthorizedMedicinalProduct skos:prefLabel ?ProductRoleType .
		} UNION {
			?TherapeuticIndication cmns-ctxdsg:isApplicableIn ?ProductRole .
			?ProductRole a idmp-mprd:InvestigationalMedicinalProduct .
		    ?ProductRole (cmns-rlcmp:isPlayedBy|^cmns-rlcmp:playsRole) ?MedicinalProduct  .
			idmp-mprd:InvestigationalMedicinalProduct rdfs:label ?ProductRoleType .
		}
	
		
  	    # Find the substance that is part of prodcut composition which defines Prodcut that is comprised of MedicinalProduct
        ?MedicinalProduct cmns-col:comprises ?Product .
        ?Product ^cmns-dsg:defines|cmns-dsg:isDefinedIn ?ProductComposition .
    	?Product ^cmns-dsg:defines|cmns-dsg:isDefinedIn ?ProductComposition .
		?SubstanceInIngredientRole cmns-pts:isRealizedIn ?ProductComposition .
		?SubstanceInIngredientRole cmns-rlcmp:isPlayedBy ?Substance .
		
		# Find most specific ingredient role
		?SubstanceInIngredientRole a/rdfs:subClassOf* ?IngredientClass .
		?IngredientClass rdfs:subClassOf* idmp-sub:Ingredient .
		FILTER NOT EXISTS { ?SubClass rdfs:subClassOf ?IngredientClass }
		?IngredientClass rdfs:label ?IngredientRoleType .
		
		# Make sure that we only return actual substances
        ?Substance a/rdfs:subClassOf* idmp-sub:Substance .

        # Optionally, get the name of the substance
        OPTIONAL{?Substance  rdfs:label  ?SubstanceLabel }
}
ORDER BY ?ProductRole ?IngredientRoleType ?Substance
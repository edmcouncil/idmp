#UC2 CQ 1.2 	In which production/manufacturing steps is substance <S> used?

PREFIX cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
PREFIX cmns-rlcmp: <https://www.omg.org/spec/Commons/RolesAndCompositions/>
PREFIX idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?ProcessStep (SAMPLE(?ProcessStepLabel) AS ?ProcessStepName)


WHERE {
    # Bind Variable substance <$Substance>
	# Example for AMLODIPINE: <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/AmlodipineExample/AmlodipineBesylate>
    BIND( uc2_cq1-2_parameter_1 AS $Substance )

  {
    # Substance directly plays a roll of a Material 
  	$MaterialRole  
      a/rdfs:subClassOf* idmp-sub:MaterialRole ;
      cmns-rlcmp:isManifestedIn ?ProcessStep ;
      cmns-rlcmp:isPlayedBy $Substance .
      
  } UNION  {
    # Substance comprises something that is used as a material
    $MaterialRole  
      a/rdfs:subClassOf* idmp-sub:MaterialRole ;
      cmns-rlcmp:isManifestedIn ?ProcessStep ;
      cmns-rlcmp:isPlayedBy [ cmns-col:comprises $Substance ]
  }
  
    # Make sure that we only return actual process steps
    ?ProcessStep  a/rdfs:subClassOf* idmp-mprd:ProcessStep .
     
    # Optionally, get the name of the Process Step
    OPTIONAL{?ProcessStep  rdfs:label  ?ProcessStepLabel }

} GROUP BY ?ProcessStep
# UC-CMC-CQ 6: Which batch <b> is released for which country <c>?
PREFIX cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
PREFIX cmns-doc:    <https://www.omg.org/spec/Commons/Documents/>
PREFIX cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-cxtdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
PREFIX cmns-id: 	<https://www.omg.org/spec/Commons/Identifiers/>
PREFIX cmns-rlcmp:  <https://www.omg.org/spec/Commons/RolesAndCompositions/>
PREFIX cmns-rga:    <https://www.omg.org/spec/Commons/RegulatoryAgencies/>
PREFIX cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:         <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT ?productBatchIdentifierValue ?marketingAuthorisationId ?country

WHERE {
    # Bind Variable medicinal product <$medicinalProduct>
	# Example for Norvasc: <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/AmlodipineExample/NorvascMedicinalProduct>
    BIND(uc_cmc_cq6_parameter_1 AS $medicinalProduct )

     $medicinalProduct cmns-doc:specifies ?physicalMedicinalProduct .
     ?physicalMedicinalProduct a idmp-mprd:PhysicalMedicinalProduct .

     ?productBatch cmns-col:isConstituentOf ?physicalMedicinalProduct .
     ?productBatch rdf:type idmp-mprd:Batch .
     ?productBatch cmns-col:comprises ?material .
     ?productBatch cmns-dsg:isSignifiedBy ?productBatchId .
     ?productBatchId cmns-txt:hasTextValue ?productBatchIdentifierValue.

     ?authorisedMedicinalProduct cmns-rlcmp:isPlayedBy $medicinalProduct .
     ?authorisedMedicinalProduct idmp-mprd:isAuthorizedForMarketingThrough ?marketingAuthorisation .
     ?marketingAuthorisationId cmns-id:identifies ?marketingAuthorisation .
     ?marketingAuthorisation cmns-cxtdsg:isApplicableIn ?jurisdiction .

     ?jurisdiction cmns-rga:hasReach ?country.
}
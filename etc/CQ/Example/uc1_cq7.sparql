# CQ 7: Which manufactured items contain substance <x> as ingredient of type “active”?
prefix cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
prefix cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
prefix cmns-rlcmp:    <https://www.omg.org/spec/Commons/RolesAndCompositions/>
prefix cmns-qtu:    <https://www.omg.org/spec/Commons/QuantitiesAndUnits/>
prefix idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/> 
prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT Distinct ?ManufacturedItem (SAMPLE(?ManufacturedItemLabel) AS ?ManufacturedItemName)

WHERE {
    # Bind the name we want into a variable
    BIND(uc1_cq7_parameter_1 AS $SUBSTANCE ) 
    { 
        # When Prod to Sub is modelled the followuing then we can query it now:
        #:manufacturedItem :isDefinedIn : ProductComposition :hasIngredient :Ingredient :isPlayedBy :SpecifiedSubstance :specializes :Substance :hasActiveMoiety :Substance
        ?ManufacturedItem rdf:type/rdfs:subClassOf* idmp-sub:ManufacturedItem ;
            cmns-dsg:isDefinedIn[
                idmp-sub:hasIngredient [
                    cmns-rlcmp:isPlayedBy [
                        cmns-qtu:specializes $SUBSTANCE
                    ]
                ]
            ] .
    } UNION {
        # Find Manufactured Items
        ?ManufacturedItem a/rdfs:subClassOf* idmp-sub:ManufacturedItem ;
            cmns-dsg:isDefinedIn [
                idmp-mprd:hasActiveIngredient [
                    cmns-rlcmp:isPlayedBy $SUBSTANCE
                ]
            ] .
    }
    # Optionally, get name of manufactured item
    OPTIONAL{?ManufacturedItem  rdfs:label  ?ManufacturedItemLabel }
    
} GROUP BY ?ManufacturedItem
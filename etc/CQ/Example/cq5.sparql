# CQ 5: In which clinical trials were the registered products administered
# Intended for running on AmlodipineExample and EuorpeanUnionClinicalTrialsREgister.rdf data

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix idmp-ra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/> 
prefix idmp-sub: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX cmns-id: <https://www.omg.org/spec/Commons/Identifiers/>
prefix cmns-ra:   <https://www.omg.org/spec/Commons/RegistrationAuthorities/> 
prefix cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
prefix idmp-mprd: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
prefix cmns-cxtdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
prefix cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>

SELECT distinct ?clinical_Trial ?trial_Name ?eudraCT_Number ?authorizedParty ?authorized_By ?trial_jurisdiction

WHERE {
  # Bind the name we want into a variable 
	BIND("Norvasc medicinal product" AS ?medicinalProductLabel )
  
  #Find the Marketing Authorization for the Medicinal Product through Authorized Medicinal Product role
  ?marketingAuthorization a idmp-mprd:MarketingAuthorization ;
            cmns-cxtdsg:appliesTo ?authorizedMedicinalProduct .
  ?authorizedMedicinalProduct cmns-pts:isPlayedBy ?medicinalProduct .
 
  # Match Investigational Medicinal Products that have the same Medicinal Product as the Marketing Authorization
  ?investigationalMedinicalProduct cmns-pts:isPlayedBy ?medicinalProduct .
  ?authorizedMedicinalProduct cmns-pts:isPlayedBy ?medicinalProduct .
  
  # Use the label of the Authorized Medicinal Product
  ?medicinalProduct rdfs:label ?medicinalProductLabel .
  
  # Find clinical trials that manages the administration of Investigational Medicinal Product from above
  ?clinical_Trial a idmp-mprd:ClinicalTrial ;
                  rdfs:label ?trial_Name ;
                  idmp-mprd:managesAdministrationOf ?investigationalMedinicalProduct .

  #EudraCT Number
  ?eudraCTNumberUri a idmp-ra:EudraClinicalTrialsNumber ;
                    cmns-txt:hasTextValue ?eudraCT_Number ;
                    cmns-id:identifies ?clinical_Trial . 
  
  # Meta-Data about the Trial Using the CTA
  ?cta_uri a idmp-mprd:ClinicalTrialAuthorization ;
           cmns-cxtdsg:appliesTo ?clinical_Trial ;
           cmns-cxtdsg:isApplicableIn ?trial_jurisdiction_uri ;
           idmp-mprd:hasAuthorizedParty ?authorizedRole ;
           idmp-mprd:hasAuthorizingParty ?authorizingRole .
  
  #Authorizing Party
  ?authorizingRole cmns-pts:isPlayedBy ?authorizedByUri .
  ?authorizedByUri rdfs:label ?authorized_By .
  
  #Authorized Party
  ?authorizedRole cmns-pts:isPlayedBy ?authorizedPartyUri .
  ?authorizedPartyUri rdfs:label ?authorizedParty .
  
  #Jursdiction
  ?trial_jurisdiction_uri rdfs:label ?trial_jurisdiction .
}
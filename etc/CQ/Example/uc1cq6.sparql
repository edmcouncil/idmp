# UC1-CQ 6: Which investigational / authorized medicinal products contain the substance <$Substance> or its active moiety <$ActiveMoiety> or any other substance related to active moiety Y?

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix idmp-ra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/> 
prefix idmp-sub: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX cmns-id: <https://www.omg.org/spec/Commons/Identifiers/>
prefix cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
prefix cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
prefix cmns-ra:   <https://www.omg.org/spec/Commons/RegistrationAuthorities/> 
prefix cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
prefix idmp-mprd: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
prefix cmns-cxtdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
prefix cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>

SELECT DISTINCT ?MedicinalProduct (SAMPLE(?MedicinalProductLabel) AS ?MedicinalProductName)

WHERE {
	# Bind Variable Active Moiety <$ActiveMoiety>
	# Example for European Union Clinical Trials Register & Amplodipine : <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/AmlodipineExample/Amlodipine>
    BIND(uc1_cq6_parameter_1 AS $ActiveMoiety )

	# Bind Variable Substance <$Substance>
	# Example for European Union Clinical Trials Register & Amplodipine : <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/AmlodipineExample/AmlodipineBesylate>
    BIND(uc1_cq6_parameter_2 AS $Substance )

 	#Find Medicinal Products that play a role of Authorized or Investigational Medicinal Products 
  	$AuthorizationRole cmns-pts:isPlayedBy ?MedicinalProduct .
	?MedicinalProduct a idmp-mprd:MedicinalProduct .
	{ $AuthorizationRole a idmp-mprd:AuthorizedMedicinalProduct} union { $AuthorizationRole a idmp-mprd:InvestigationalMedicinalProduct } .
  	?MedicinalProduct cmns-col:comprises $PharmaceuticalProduct .
  	
	{
		# Find pharmaceutical products that have $Substance as an Ingredient 
		$PharmaceuticalProduct ^cmns-dsg:defines|cmns-dsg:isDefinedIn $ProductComposition .
		$SubstanceInIngredientRole cmns-pts:isRealizedIn $ProductComposition .
		$SubstanceInIngredientRole cmns-pts:isPlayedBy $Substance .
	} UNION {
  		# Find other medicinal products that contain substances with $ActiveMoiety
    	$PharmaceuticalProduct ^cmns-dsg:defines|cmns-dsg:isDefinedIn $ProductComposition .
		$SubstanceInIngredientRole cmns-pts:isRealizedIn $ProductComposition .
		$SubstanceInIngredientRole cmns-pts:isPlayedBy $SubstanceSharingMoiety .
    	$SubstanceSharingMoiety  idmp-sub:hasActiveMoiety $ActiveMoiety .
  	} UNION {
  		# Find other medicinal products that have substances related to $ActiveMoiety
    	$PharmaceuticalProduct ^cmns-dsg:defines|cmns-dsg:isDefinedIn $ProductComposition .
		$SubstanceInIngredientRole cmns-pts:isRealizedIn $ProductComposition .
		$SubstanceInIngredientRole cmns-pts:isPlayedBy $SubstanceRelatedToMoiety .
      	$RelatedToProperty rdfs:subPropertyOf* idmp-sub:isRelatedSubstanceTo .
    	$SubstanceRelatedToMoiety  $RelatedToProperty $ActiveMoiety .	
  	}
    
    # Optionally, get the name of the Medicinal Product
 	OPTIONAL{?MedicinalProduct  rdfs:label  ?MedicinalProductLabel }

} GROUP BY ?MedicinalProduct
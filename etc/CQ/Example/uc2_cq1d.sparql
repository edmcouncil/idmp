#UC2-CQ 1d: In which packaging is substance <$Substance> used?

PREFIX cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
PREFIX idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?Packaging (SAMPLE(?PackagingLabel) AS ?PackagingName)

where {
    # Bind Variable substance <$Substance>
	# Example for AMLODIPINE: <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/AmlodipineExample/PolyvinylChloride>
    BIND( uc2_cq1d_parameter_1 AS $Substance )

    ?Packaging a/rdfs:subClassOf* idmp-mprd:MedicinalProductContainer .
    # Optionally, get the name of the substance
    OPTIONAL{?Packaging  rdfs:label  ?PackagingLabel }

    # A package is made of a single material no components, e.g. the paperboard box
    # or the package is made of components, e.g. the form and lidding of a blister
    # cmns-col:comprises may have subproperties for material and alternate material
    # IDMP allows any material from a controlled vocabulary, that need not be substances in the defintion of IDMP
    ?Packaging idmp-mprd:hasPackageComponent*/cmns-col:comprises $Substance .
	
} GROUP BY ?Packaging
# UC1-CQ 9: What is the molecular structure of <$SUBSTANCE>?"
PREFIX cmns-cls:    <https://www.omg.org/spec/Commons/Classifiers/isClassifiedBy/>
PREFIX cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT DISTINCT ?StructuralRepresentationType ?StructuralRepresentationText

WHERE {
    # Bind Variable substance <$SUBSTANCE>
	# Example for AMLODIPINE: <https://spor.ema.europa.eu/v2/SubstanceDefinition/100000090079>
  	# Example for Terlipressin: <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/TerlipressinExample/TerlipressinAcetate>
    BIND(uc1_cq9_parameter_1 AS $SUBSTANCE )

	  # Link Substance to molecular structure
    $SUBSTANCE idmp-sub:hasDefiningStructure ?Structure .
	
	{ 
		?Structure idmp-sub:hasSMILES ?SMILES .
		?SMILES idmp-sub:hasSMILESValue ?StructuralRepresentationText .
		BIND( idmp-sub:StructuralRepresentationType-SMILES AS ?StructuralRepresentationType)
	} 
	UNION
	{ 
		?Structure idmp-sub:hasInChIKey ?InChIKey .
		?InChIKey idmp-sub:hasInChIKeyValue ?StructuralRepresentationText .
		BIND( idmp-sub:StructuralRepresentationType-InChIKey AS ?StructuralRepresentationType)
	} 
	UNION
	{ 
		?Structure idmp-sub:hasInChI ?InChI .
		?InChI idmp-sub:hasInChIValue ?StructuralRepresentationText .
		BIND( idmp-sub:StructuralRepresentationType-InChI AS ?StructuralRepresentationType)
	}
	UNION
	{ 
		?Structure cmns-dsg:isDescribedBy ?StructuralRepresentation .
		# Make sure that we only return structural representations
		?StructuralRepresentation a/rdfs:subClassOf* idmp-sub:StructuralRepresentation .
		?StructuralRepresentation idmp-sub:hasStructuralRepresentationText ?StructuralRepresentationText .
		OPTIONAL {
		   ?StructuralRepresentation cmns-cls:isClassifiedBy    ?StructuralRepresentationType .
	       ?StructuralRepresentationType a/rdfs:subClassOf* idmp-sub:StructuralRepresentationType
		}
	}
} 
ORDER BY ?StructuralRepresentationType ?StructuralRepresentationText

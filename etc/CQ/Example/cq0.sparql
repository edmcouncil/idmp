PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX cmns-dsg:	<https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-txt: 	<https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX cmns-id:	    <https://www.omg.org/spec/Commons/Identifiers/>
PREFIX owl:         <http://www.w3.org/2002/07/owl#>
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:        <http://www.w3.org/2004/02/skos/core#>


#Search substance by name case-insensitive

SELECT DISTINCT ?Substance
WHERE {
	# Bind Variable SubstanceNameSearch
    # Example  "Amlodipine" for $SubstanceNameSearch
    BIND ( cq0_parameter_1 AS $SubstanceNameSearch )
    
    ?SubstanceAlias rdf:type/rdfs:subClassOf* idmp-sub:Substance .
	?Substance (owl:sameAs|^owl:sameAs)* ?SubstanceAlias .

	# the standard IDMP-O access to the substance label
	OPTIONAL {
      ?Substance idmp-sub:hasSubstanceName/idmp-sub:hasSubstanceNameValue ?SubstanceName .
      FILTER ( LCASE(STR(?SubstanceName)) = LCASE(STR($SubstanceNameSearch)))
    }

	OPTIONAL {
      ?Substance (^cmns-dsg:isNameOf|cmns-dsg:hasName)/cmns-txt:hasTextValue ?SubstanceName .
      FILTER (LCASE(STR(?SubstanceName)) = LCASE(STR($SubstanceNameSearch)))
    }
	
	# using skos or rdfs:label
    OPTIONAL {
      ?SimpleLabelProperty rdfs:subPropertyOf* rdfs:label .
      ?Substance ?SimpleLabelProperty ?SubstanceLabel .
      FILTER (LCASE(STR(?SubstanceLabel)) = LCASE($SubstanceNameSearch))
    }
    

  FILTER (BOUND(?SubstanceLabel) || BOUND(?SubstanceName))
}
ORDER BY ?Substance
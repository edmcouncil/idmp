# CQ4.1: Further relevant codes
PREFIX cmns-id: 	<https://www.omg.org/spec/Commons/Identifiers/>
prefix cmns-ra:   	<https://www.omg.org/spec/Commons/RegistrationAuthorities/>
prefix cmns-pts:    <https://www.omg.org/spec/Commons/PartiesAndSituations/>
prefix cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>
prefix idmp-ra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/> 
prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
prefix rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?substance ?code ?registryLabel ?authorityLabel 

WHERE {
	# Bind the name we want into a variable 
	BIND("amlodipine" AS ?substanceLabel ) 

    # Find the substance that is a substance or a subclass of susbtance:
    ?type rdfs:subClassOf* idmp-sub:Substance .
    ?substance rdf:type ?type;
   
    # has a label that we are looking for
    rdfs:label ?substanceLabel .

    # Find the Registered Identifier or a subclass of registered identifier:
    ?idType rdfs:subClassOf*  cmns-ra:RegisteredIdentifier.
    ?identifier rdf:type  ?idType;

        # Identifies the substance
        cmns-id:identifies ?substance ;

        # has text value for the Code/Identifier
        cmns-txt:hasTextValue ?code ;

        # Registered in a Registry
       	cmns-ra:isRegisteredIn ?registry .
  
  #Registry has a label
  ?registry rdfs:label ?registryLabel .
  
  #Registry may have an authority that manages it
  Optional{ 
    ?authority cmns-ra:manages ?registry .
    ?authority cmns-pts:isPlayedBy ?agency .
  	?agency rdfs:label ?authorityLabel .
  }
}
# CQ 2: What is the active moiety of <substance y>?

prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?activeMoiety ?property ?activeMoietyLabel

where {
	# Bind the name we want into a variable 
	  BIND("amlodipine mesylate monohydrate" AS ?substanceLabel )
	  BIND(rdfs:label as ?property )

  	# Find a substance or subclass of substance
  	?substanceType rdfs:subClassOf* idmp-sub:Substance .
	?substanceUri a ?substanceType .

    # With a label for substance y
  	?substanceUri rdfs:label ?substanceLabel .
  	
  	# Link the substance to the active moiety
  	?substanceUri idmp-sub:hasActiveMoiety ?activeMoiety .
  	?activeMoiety a ?activeMoietyType .
  	?activeMoiety rdfs:label ?activeMoietyLabel .
}
# CQ03.1: What is the basis of strength for <substance x> in <product y>?

prefix cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
prefix idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix idmp-ucum: 	<https://spec.pistoiaalliance.org/idmp/ontology/EXT/Extensions/UnifiedCodeForUnitsOfMeasure/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
prefix cmns-qtu: <https://www.omg.org/spec/Commons/QuantitiesAndUnits/>
prefix cq3:	<https://spec.pistoiaalliance.org/idmp/cq3/>	

 

SELECT distinct ?referenceSubstance ?property1 ?referenceSubstanceLabel ?property2 ?referenceStrengthString

WHERE {
    BIND("Norvasc" AS ?productLabel )
    BIND("amlodipine mesylate monohydrate" AS ?substanceLabel )
	BIND(rdfs:label as ?property1)
	BIND(cq3:hasReferenceStrengthString as ?property2)

  
  	# Start with the pharmaceutical product and link to its active ingredient from the product constituency
  	?pharmaceuticalProduct rdfs:label ?productLabel .
  	?productConstituency cmns-dsg:defines ?pharmaceuticalProduct .
	?productConstituency idmp-mprd:hasActiveIngredient ?activeIngredient .
  
  	# Get the reference substance and strength from the active ingredient
  	?activeIngredient idmp-mprd:hasReferenceSubstance ?referenceSubstance .
  	?referenceSubstance rdfs:label ?referenceSubstanceLabel .
  
  	# Get the Numerator and  Denominator information from the reference strength
  	?activeIngredient idmp-mprd:hasReferenceStrength ?referenceStrength .
  	?referenceStrength cmns-qtu:hasDenominator ?strengthDenominator .
  	?strengthDenominator cmns-qtu:hasNumericValue ?strengthDenominatorValue .
  	?strengthDenominator cmns-qtu:hasMeasurementUnit ?strengthDenominatorUnit .
    ?referenceStrength cmns-qtu:hasNumerator ?strengthNumerator .  
  	?strengthNumerator cmns-qtu:hasNumericValue ?strengthNumeratorValue .
  	?strengthNumerator cmns-qtu:hasMeasurementUnit ?strengthNumeratorUnit .
  	?strengthNumeratorUnit rdfs:label ?strengthNumeratorUnitLabel .
  	?strengthDenominatorUnit rdfs:label ?strengthDenominatorUnitLabel .
  	
  
	# Bind the Numerator and  Denominator into a concatianated variables for readability
  	BIND (concat(str(?strengthNumeratorValue)," ",?strengthNumeratorUnitLabel) as ?strengthNumeratorString )
  	BIND (concat(str(?strengthDenominatorValue)," ",?strengthDenominatorUnitLabel) as ?strengthDenominatorString )
  	BIND (concat(?strengthNumeratorString," / ", ?strengthDenominatorString) as ?referenceStrengthString)
}

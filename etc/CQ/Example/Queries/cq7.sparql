# CQ 7: Which manufactured items contain substance <x> as ingredient of type “active”?

prefix cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
prefix cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
prefix cmns-pts:    <https://www.omg.org/spec/Commons/PartiesAndSituations/>
prefix idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
prefix idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT distinct ?manufacturedItem ?manufacturedItemLabel

WHERE {
	# Bind the name we want into a variable 
	BIND("terlipressin acetate" AS ?substanceLabel )

  	# Find Manufactured Items
	?manufacturedItemType rdfs:subClassOf* idmp-sub:ManufacturedItem .
  	?manufacturedItem rdf:type ?manufacturedItemType .
  	?manufacturedItem rdfs:label ?manufacturedItemLabel .
  
  	# Find the pharmaceutical products that comprise the manufactured item
	?manufacturedItem cmns-col:comprises ?pharmaceuticalProduct .
  
  	# product constituency link substance as an ingredient to the pharmaceutical product
  	?productConstituency cmns-dsg:defines ?pharmaceuticalProduct .
  	?productConstituency idmp-mprd:hasActiveIngredient ?substanceInActiveIngredient .
  	
  	# Substance as an active ingredient
  	?substanceInActiveIngredient cmns-pts:isPlayedBy ?substance .
  	?substance rdfs:label  ?substanceLabel .
}
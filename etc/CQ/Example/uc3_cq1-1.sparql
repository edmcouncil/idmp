#UC3 CQ 1.1 	What substance <S> is part of an authorized medicinal product that is approved for a given therapeutic indication indicated by a MedDRA ID <I>?

PREFIX cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
PREFIX cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>
PREFIX idmp-eura:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/EuropeanJurisdiction/EuropeanRegistrationAuthorities/>
PREFIX idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX idmp-phprd: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11616-PharmaceuticalProducts/> 
PREFIX cmns-dsg:   <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-ctxdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
PREFIX cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>
PREFIX cmns-id: <https://www.omg.org/spec/Commons/Identifiers/>
PREFIX cmns-txt: <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX idmp-ra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/>
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>

SELECT DISTINCT ?Substance ?SubstanceLabel
WHERE {

        # Bind Variable MedDRA ID <I>
        # Example "10020772" for $MedDraId
        BIND( uc3_cq1-1_parameter_1  AS $MedDRAId)

        # Find Nedicinal Product that is approved for the TherapeuticIndication having MedDRA ID
		?TherapeuticIndication a idmp-mprd:TherapeuticIndication . 
		?TherapeuticIndication idmp-mprd:hasIndicationAsDiseaseSymptomProcedure ?MedicalConditionClassifier.
		?MedicalConditionClassifier a idmp-eura:MedDRATerm;
		   (cmns-id:isIdentifiedBy|^cmns-id:identifies) ?MedDRAIdentifier .
		?MedDRAIdentifier cmns-txt:hasTextValue $MedDRAId .
	
		{
			?TherapeuticIndication (idmp-mprd:isTherapeuticIndicationFor|^idmp-mprd:hasTherapeuticIndication) ?MedicinalProduct .
		} UNION {
			?TherapeuticIndication cmns-ctxdsg:isApplicableIn ?AuthorizedMedicinalProduct .
			?AuthorizedMedicinalProduct a idmp-mprd:AuthorizedMedicinalProduct .
		    ?AuthorizedMedicinalProduct (cmns-pts:isPlayedBy|^cmns-pts:playsRole) ?MedicinalProduct  .
		} UNION {
			?TherapeuticIndication cmns-ctxdsg:isApplicableIn ?InvestigationalMedicinalProduct .
			?InvestigationalMedicinalProduct a idmp-mprd:InvestigationalMedicinalProduct .
		    ?InvestigationalMedicinalProduct (cmns-pts:isPlayedBy|^cmns-pts:playsRole) ?MedicinalProduct  .
		}
	
		
  	    # Find the substance that is part of prodcut composition which defines Prodcut that is comprised of MedicinalProduct
        ?MedicinalProduct cmns-col:comprises ?Product .
        ?Product ^cmns-dsg:defines|cmns-dsg:isDefinedIn ?ProductComposition .
    	?Product ^cmns-dsg:defines|cmns-dsg:isDefinedIn ?ProductComposition .
		?SubstanceInIngredientRole cmns-pts:isRealizedIn ?ProductComposition .
		?SubstanceInIngredientRole cmns-pts:isPlayedBy ?Substance .

		# Make sure that we only return actual substances
        ?Substance a/rdfs:subClassOf* idmp-sub:Substance .

        # Optionally, get the name of the substance
        OPTIONAL{?Substance  rdfs:label  ?SubstanceLabel }
}
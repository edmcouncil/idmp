# UC1-CQ 3:  What are the products that contain substances with common active moiety <active moiety x>?
PREFIX cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-rlcmp:  <https://www.omg.org/spec/Commons/RolesAndCompositions/>
PREFIX cmns-qtu:    <https://www.omg.org/spec/Commons/QuantitiesAndUnits/>
PREFIX idmp-mprd:   <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX idmp-sub:    <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX cmns-pts:    <https://www.omg.org/spec/Commons/PartiesAndSituations/>
PREFIX rdf:         <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:        <http://www.w3.org/2000/01/rdf-schema#>
 
SELECT DISTINCT ?Product (SAMPLE(?ProductLabel) AS ?ProductName)

WHERE {
    # Bind Variable Substance <$ActiveMoiety>
	# Example for AMLODIPINE: <https://spor.ema.europa.eu/v2/SubstanceDefinition/100000085259>
  	# Example for Terlipressin: <https://spec.pistoiaalliance.org/idmp/ontology/EXT/Examples/TerlipressinExample/Terlipressin>
    BIND(uc1_cq3_parameter_1 AS $ActiveMoiety )
  
    ?Moiety a/rdfs:subClassOf* idmp-sub:Moiety .
   	?Moiety cmns-qtu:specializes* $ActiveMoiety .
  
    {
        # Use the shortcut (isActiveMoietyOf or hasActiveMoiety)
	    ?Substance a/rdfs:subClassOf* idmp-sub:Substance .
    	?Substance  cmns-qtu:specializes*/(^idmp-sub:isActiveMoietyOf|idmp-sub:hasActiveMoiety) ?Moiety .
  	}
	UNION {
        # Use the $ActiveMoiety in a role for composition that defines a substance.
	    ?Substance a/rdfs:subClassOf* idmp-sub:Substance .
    	?Substance cmns-qtu:specializes*/(cmns-dsg:isDefinedIn|^cmns-dsg:defines) ?SubstanceComposition .
       	?SubstanceComposition a/rdfs:subClassOf* idmp-sub:SubstanceComposition .
        ?SubstanceComposition cmns-rlcmp:hasRole ?ActiveMoietyRole .
    	?ActiveMoietyRole a/rdfs:subClassOf* idmp-sub:ActiveMoietyRole ;
              	cmns-rlcmp:isPlayedBy ?Moiety .
    }
    {
  		?IngredientRoleType rdfs:subClassOf* idmp-sub:ActiveIngredient
  	} UNION {
   		?IngredientRoleType  rdfs:subClassOf* idmp-sub:InactiveIngredient
   	}

  	?IngredientRole a ?IngredientRoleType .
   	{
    	?IngredientRole cmns-rlcmp:isPlayedBy ?Substance .
  	} UNION {
    	?IngredientRole cmns-rlcmp:isPlayedBy ?Moiety .
  	}
   	?IngredientRole cmns-pts:isRealizedIn|^cmns-rlcmp:hasRole|^idmp-mprd:hasIngredient|^idmp-mprd:hasActiveIngredient ?ProductComposition .
   	?ProductComposition a/rdfs:subClassOf* idmp-mprd:ProductComposition .
	?ProductComposition ^cmns-dsg:isDefinedIn|cmns-dsg:defines ?Product .
  # Optionally, get the name of the Product
   OPTIONAL{?Product  rdfs:label  ?ProductLabel }
  
} 
GROUP BY ?Product
ORDER BY ?Product

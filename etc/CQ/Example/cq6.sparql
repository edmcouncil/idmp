# Which investigational / authorized medicinal products contain the substance X or its active moiety Y or any other substance related to active moiety Y?

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix idmp-ra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/> 
prefix idmp-sub: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX cmns-id: <https://www.omg.org/spec/Commons/Identifiers/>
prefix cmns-col:    <https://www.omg.org/spec/Commons/Collections/>
prefix cmns-dsg:    <https://www.omg.org/spec/Commons/Designators/>
prefix cmns-ra:   <https://www.omg.org/spec/Commons/RegistrationAuthorities/> 
prefix cmns-txt:    <https://www.omg.org/spec/Commons/TextDatatype/>
prefix rdfs:        <http://www.w3.org/2000/01/rdf-schema#> 
prefix idmp-mprd: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
prefix cmns-cxtdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
prefix cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>

SELECT distinct ?medicinalProduct ?medicinalProductLabel ?roleLabel

WHERE {
  	#Find Medicinal Products that play a role of Authorized or Investigational Medicinal Products 
  	?role cmns-pts:isPlayedBy ?medicinalProduct .
  	?role rdfs:label ?roleLabel .
  	{?role a idmp-mprd:AuthorizedMedicinalProduct} union {?role a idmp-mprd:InvestigationalMedicinalProduct} 
	  ?medicinalProduct a idmp-mprd:MedicinalProduct .
  	?medicinalProduct rdfs:label ?medicinalProductLabel .
  	?medicinalProduct cmns-col:comprises ?pharmaceuticalProduct .
  	{
  	# Find the substance X through the product constituency
  	?pharmaceuticalProduct rdfs:label ?pharmaceuticalProductLabel .
  	?productConstituency cmns-dsg:defines ?pharmaceuticalProduct .
 	  ?substanceInIngredientRole cmns-pts:isRealizedIn ?productConstituency .
  	?substanceInIngredientRole cmns-pts:isPlayedBy ?substance .
  	?substance rdfs:label "amlodipine besylate" .
  	
  	# Find substance X's activeMoiety Y
  	?substance idmp-sub:hasActiveMoiety ?activeMoiety .
  	?activeMoiety rdfs:label "amlodipine" .
	}
    # Find other medicinal products that contain substances with active moiety Y
    union {
    	?pharmaceuticalProduct rdfs:label ?pharmaceuticalProductLabel .
  		?productConstituency cmns-dsg:defines ?pharmaceuticalProduct .
    	?substanceSharingMoietyInIngredientRole cmns-pts:isRealizedIn ?otherProductConstituency .
  		?substanceSharingMoietyInIngredientRole cmns-pts:isPlayedBy ?substanceSharingMoiety .
      # Link the other substances through the hasActiveMoiety ?activeMoiety ( in this case amlopidine )
    	?substanceSharingMoiety  idmp-sub:hasActiveMoiety ?activeMoiety .
    }
    # Find other medicinal products that have substances related to active Moiety Y
    union {
    	?pharmaceuticalProduct rdfs:label ?pharmaceuticalProductLabel .
  		?productConstituency cmns-dsg:defines ?pharmaceuticalProduct .
    	?substanceSharingMoietyInIngredientRole cmns-pts:isRealizedIn ?otherProductConstituency .
  		?substanceSharingMoietyInIngredientRole cmns-pts:isPlayedBy ?substanceSharingMoiety .
      # Link the other substances through isRelatedTo property or subproperties.
      ?relatedToProperty rdfs:subPropertyOf* cmns-col:isRelatedTo .
    	?substanceSharingMoiety  ?relatedToProperty ?activeMoiety .
    }
}
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX cmns-dsg: <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-txt: <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX cmns-id: <https://www.omg.org/spec/Commons/Identifiers/>
PREFIX cmns-cls: <https://www.omg.org/spec/Commons/Classifiers/>
PREFIX idmp-sub: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX idmp-ra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/>
PREFIX idmp-eura: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/EuropeanJurisdiction/EuropeanRegistrationAuthorities/>
PREFIX idmp-amra: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/NorthAmericanJurisdiction/NorthAmericanRegistrationAuthorities/>
PREFIX cmns-ra: <https://www.omg.org/spec/Commons/RegistrationAuthorities/>
PREFIX spor-st: <https://spor.ema.europa.eu/v1/lists/100000075826/terms/>

GENERATE {
  <https://spor.ema.europa.eu/v1/lists/SubstanceDefinition/terms/{ ?SMS_ID }>
    a ?idmpSubstanceType, ?sporRMSSubstanceType ;
    skos:prefLabel ?prefLabel ;
    skos:altLabel ?altLabel ;
    dct:created ?Created_Date ;
    dct:modified ?Last_Updated_Date ;
    cmns-dsg:hasName ?nameURI ;
    cmns-id:isIdentifiedBy <https://data.pistoiaalliance.org/id/EVMPD/{ ?External_Code_XEVMPD }> ;
    cmns-id:isIdentifiedBy <https://data.pistoiaalliance.org/id/FDA_UNII/{ ?External_Code_UNII }> ;
    skos:exactMatch <https://gsrs.ncats.nih.gov/api/v1/substances/{ ?External_Code_UNII }> ;
    cmns-cls:isClassifiedBy ?idmpSubstanceDomain ;
    .

  ?nameURI a idmp-sub:SubstanceName ;
    cmns-txt:hasTextValue ?Substance_Name ;
    dct:source ?sourceAuthority ;
    .

  <https://data.pistoiaalliance.org/id/EVMPD/{ ?External_Code_XEVMPD }>
    a idmp-eura:EudraVigilanceCode ;
    cmns-id:identifies <https://spor.ema.europa.eu/v1/lists/SubstanceDefinition/terms/{ ?SMS_ID }> ;
    cmns-txt:hasTextValue ?External_Code_XEVMPD ;
    .
}
ITERATOR iter:CSV(<sms-substances-list.csv>) AS
?SMS_ID
?Substance_Name
?Is_Preferred_Name
?Language
?Name_Source
?Substance_Domain
?Status
?Substance_Type
?Molecular_Formula
?Molecular_Weight
?Inchikey
?Comment
?Created_Date
?Last_Updated_Date
?External_Code_XEVMPD
?External_Code_SVG
?External_Code_UNII
WHERE {
  BIND(
    IF(?Language = "Bulgarian", "bg",
    IF(?Language = "Croatian", "hr",
    IF(?Language = "Czech", "cs",
    IF(?Language = "Danish", "da",
    IF(?Language = "Dutch", "nl",
    IF(?Language = "English", "en",
    IF(?Language = "Estonian", "et",
    IF(?Language = "Finnish", "fi",
    IF(?Language = "French", "fr",
    IF(?Language = "German", "de",
    IF(?Language = "Greek, Modern (1453-)", "el",
    IF(?Language = "Hungarian", "hu",
    IF(?Language = "Icelandic", "is",
    IF(?Language = "Irish", "ga",
    IF(?Language = "Italian", "it",
    IF(?Language = "Latin", "la",
    IF(?Language = "Latvian", "lv",
    IF(?Language = "Lithuanian", "lt",
    IF(?Language = "Maltese", "mt",
    IF(?Language = "Norwegian", "no",
    IF(?Language = "Polish", "pl",
    IF(?Language = "Portuguese", "pt",
    IF(?Language = "Romanian", "ro",
    IF(?Language = "Slovak", "sk",
    IF(?Language = "Slovenian", "sl",
    IF(?Language = "Spanish", "es",
    IF(?Language = "Swedish", "sv",
    ?Language )))))))))))))))))))))))))))
    AS ?Normalized_Language )

  BIND( STRLANG("{?Substance_Name}","{?Normalized_Language}") AS ?Substance_Name_LangString )
  BIND( IF(?Is_Preferred_Name = "True", ?Substance_Name_LangString, ?unbound) AS ?prefLabel )
  BIND( IF(?Is_Preferred_Name = "False", ?Substance_Name_LangString, ?unbound) AS ?altLabel )

  BIND(
    IF(?Substance_Type = "Chemical", idmp-sub:ChemicalSubstance,
    IF(?Substance_Type = "Mixture", idmp-sub:Mixture,
    IF(?Substance_Type = "Nucleic acid", idmp-sub:NucleicAcidSubstance,
    IF(?Substance_Type = "Polymer", idmp-sub:PolymerSubstance,
    IF(?Substance_Type = "Protein", idmp-sub:ProteinSubstance,
    IF(?Substance_Type = "Specified Substance Group 1", idmp-sub:SpecifiedSubstanceGroup1,
    IF(?Substance_Type = "Specified Substance Group 2", idmp-sub:SpecifiedSubstanceGroup2,
    IF(?Substance_Type = "Specified Substance Group 3", idmp-sub:SpecifiedSubstanceGroup3,
    IF(?Substance_Type = "Specified Substance Group 4", idmp-sub:SpecifiedSubstanceGroup4,
    IF(?Substance_Type = "Structurally Diverse - Allergen", idmp-sub:StructurallyDiverseSubstance,
    IF(?Substance_Type = "Structurally Diverse - Cell therapy", idmp-sub:StructurallyDiverseSubstance,
    IF(?Substance_Type = "Structurally Diverse - Herbal", idmp-sub:StructurallyDiverseSubstance,
    IF(?Substance_Type = "Structurally Diverse - Other", idmp-sub:StructurallyDiverseSubstance,
    IF(?Substance_Type = "Structurally Diverse - Plasma derived", idmp-sub:StructurallyDiverseSubstance,
    IF(?Substance_Type = "Structurally Diverse - Vaccine", idmp-sub:StructurallyDiverseSubstance,
    ?Substance_Type )))))))))))))))
    AS ?idmpSubstanceType )

  BIND(
    IF(?Substance_Type = "Chemical", spor-st:100000075670,
    IF(?Substance_Type = "Mixture", spor-st:200000005023,
    IF(?Substance_Type = "Nucleic acid", spor-st:200000005035,
    IF(?Substance_Type = "Polymer", spor-st:200000005022,
    IF(?Substance_Type = "Protein", spor-st:200000005020,
    IF(?Substance_Type = "Specified Substance Group 1", spor-st:200000005031,
    IF(?Substance_Type = "Specified Substance Group 2", spor-st:200000005032,
    IF(?Substance_Type = "Specified Substance Group 3", spor-st:200000005033,
    IF(?Substance_Type = "Specified Substance Group 4", spor-st:200000005034,
    IF(?Substance_Type = "Structurally Diverse - Allergen", spor-st:200000005026,
    IF(?Substance_Type = "Structurally Diverse - Cell therapy", spor-st:200000005029,
    IF(?Substance_Type = "Structurally Diverse - Herbal", spor-st:200000005025,
    IF(?Substance_Type = "Structurally Diverse - Other", spor-st:200000005030,
    IF(?Substance_Type = "Structurally Diverse - Plasma derived", spor-st:200000005024,
    IF(?Substance_Type = "Structurally Diverse - Vaccine", spor-st:200000005027,
    ?Substance_Type )))))))))))))))
    AS ?sporRMSSubstanceType )

  BIND(
    IF(?Substance_Domain = "Human use", <https://spor.ema.europa.eu/v1/lists/100000000004/terms/100000000012>,
    IF(?Substance_Domain = "Veterinary use", <https://spor.ema.europa.eu/v1/lists/100000000004/terms/100000000013>,
    IF(?Substance_Domain = "Human and Veterinary use", <https://spor.ema.europa.eu/v1/lists/100000000004/terms/100000000014>,
    ?unbound )))
    AS ?idmpSubstanceDomain )

  BIND( IF(?Name_Source = "", ?unbound, <https://spor.ema.europa.eu/v1/lists/SubstanceDefinition/terms/{ ?SMS_ID }/{ StrUUID() }>) AS ?nameURI )
  BIND(
    IF(?Name_Source = "CHEBI", idmp-eura:ChemicalEntitiesOfBiologicalInterest, # valid
    IF(?Name_Source = "CHEMICAL ABSTRACT SERVICE", idmp-amra:ChemicalAbstractsService, # valid
    IF(?Name_Source = "EUROPEAN CHEMICALS AGENCY (ECHA)", idmp-eura:EuropeanChemicalsAgency, # valid
    IF(?Name_Source = "FDA SUBSTANCE REGISTRATION SYSTEM", idmp-amra:FoodAndDrugAdministrationRegistrationAuthority, # valid
    IF(?Name_Source = "INN", idmp-ra:InternationalNonproprietaryName, # valid
    IF(?Name_Source = "ITIS (INTEGRATED TAXONOMIC INFORMATION SYSTEM)", idmp-amra:IntegratedTaxonomicInformationSystem, # valid
    IF(?Name_Source = "PUBCHEM", cmns-ra:PubChem, # valid
    IF(?Name_Source = "THE MERCK INDEX 20TH EDITION", idmp-ra:MerckIndexOnline, # valid
    IF(?Name_Source = "USP", idmp-ra:USPharmacopeiaMedicinesCompendium, # valid
    IF(?Name_Source = "WHO", idmp-ra:WorldHealthOrganization, # valid
    ?unbound ))))))))))
    AS ?sourceAuthority )
}

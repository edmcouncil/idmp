PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX idmp-mprd: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11615-MedicinalProducts/>
PREFIX idmp-phdf: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11239-PharmaceuticalDoseForms/>
PREFIX idmp-uom: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11240-UnitsOfMeasurement/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX idmp-chg: <https://spec.pistoiaalliance.org/idmp/ontology/META/ChangeManagement/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX cmns-cls: <https://www.omg.org/spec/Commons/Classifiers/>
PREFIX cmns-reg: <https://www.omg.org/spec/Commons/RegistrationAuthorities/>
PREFIX cmns-txt: <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX cmns-id: <https://www.omg.org/spec/Commons/Identifiers/>
PREFIX idmp-edqm:  <https://data.pistoiaalliance.org/idmp/edqm/>

GENERATE {
  <https://data.pistoiaalliance.org/idmp/edqm/{ ?class }> rdf:type owl:Ontology ;
        rdfs:label                 ?IntroText ;
        dct:license                "http://opensource.org/licenses/MIT"^^xsd:anyURI ;
        dct:source                 "This data originates from EDQM Standard Terms database (http://standardterms.edqm.eu), it is reproduced with permission of the European Directorate for the Quality of Medicines & HealthCare, Council of Europe (EDQM). This data was retrieved from the Standard Terms database on 19.07.2023. This data may have changed since then, the EDQM Standard Terms database is not a static list." ;
        idmp-chg:hasMaturityLevel  idmp-chg:Informative ;
        .

  # Main Entity
  <https://data.pistoiaalliance.org/idmp/edqm/{ ?FullID }>
    a ?IDMP_Class;
    skos:prefLabel ?Name_english;
    cmns-id:isIdentifiedBy <https://data.pistoiaalliance.org/id/EDQM/{ ?FullID }>;
    skos:definition ?Definition_english;
    dct:modified ?Last_updated;
    dct:created ?Created_Date ;
    rdf:comment ?comment_final;
    cmns-cls:isClassifiedBy ?IdmpSubstanceDomain;
    ?link_predicate <https://data.pistoiaalliance.org/idmp/edqm/{ ?FullID_Link }>;
    .

  # Identifier
  <https://data.pistoiaalliance.org/id/EDQM/{ ?FullID }>
    a cmns-reg:RegisteredIdentifier;
    cmns-txt:hasTextValue ?FullID;
    cmns-id:identifies <https://data.pistoiaalliance.org/idmp/edqm/{ ?FullID }>;
    cmns-reg:isRegisteredIn <https://spec.pistoiaalliance.org/idmp/ontology/ISO/EuropeanJurisdiction/EuropeanRegulatoryAgencies/EuropeanDirectorateForTheQualityOfMedicinesAndHealthcare>
    .

  # Creating translations
  GENERATE{
    <https://data.pistoiaalliance.org/idmp/edqm/{ ?FullID }>
      skos:prefLabel ?Lang_label;
      .
  }
  ITERATOR iter:JSONSurfer(?translations,
    "$[*]",
    "$.language",
    "$.region",
    "$.term",
    "$.version",
    "$.version_date"
    ) AS
  ?I1_translations
  ?language
  ?region
  ?lang_term
  ?lang_version
  ?lang_version_date

  BIND(IF(?lang_term,STRLANG("{?lang_term}","{?language}"),?unbound) AS ?Lang_label)
  .

  # Creating links
  GENERATE {
    GENERATE{
      <https://data.pistoiaalliance.org/idmp/edqm/{ ?FullID }>
        ?link_predicate <https://data.pistoiaalliance.org/idmp/edqm/{ ?FullID_Link }>;
    }
    ITERATOR iter:JSONListKeys(?links) AS ?key

    BIND(fun:JSONPath(?links, '$.{?key}' ) AS ?iterableLinks)

    ITERATOR iter:JSONSurfer(?iterableLinks,
      "$[*]",
      "$.code",
      "$.term"
    ) AS
    ?IT1_link
    ?link_code
    ?link_term

    BIND(IF(?class = "PDF" && ?key = "AME", idmp-phdf:hasAdministrationMethod,
    IF(?class = "PDF" && ?key = "BDF", idmp-phdf:hasBasicDoseForm,
    IF(?class = "PDF" && ?key = "ISI", idmp-phdf:hasIntendedSite,
    IF(?class = "PDF" && ?key = "RCA", idmp-phdf:hasReleaseCharacteristic,
    IF(?class = "PDF" && ?key = "TRA", idmp-phdf:hasTransformationProcedure,
    skos:related))))) AS ?link_predicate)

    BIND(CONCAT(?key,"-",?link_code)  AS ?FullID_Link)
    .
  } WHERE {
    BIND(fun:JSONPath(?I1, '$.links') AS ?links)
    FILTER(bound(?links))
  }
  .
}
ITERATOR iter:JSONSurfer(<?source>,
  "$[*]",
  "$.class",
  "$.code",
  "$.monograph",
  "$.domain",
  "$.status",
  "$.creation_date",
  "$.modification_date",
  "$.english",
  "$.definition",
  "$.comment",
  "$.req_auth",
  "$.auth_req_id",
  "$.auth_reg_term",
  "$.edqm_req_id",
  "$.version",
  "$.version_date",
  "$.translations"
  ) AS
?I1
?class
?code
?monograph
?domain
?status
?creation_date
?modification_date
?term
?definition
?comment
?req_auth
?auth_req_id
?auth_reg_term
?edqm_req_id
?version
?version_date
?translations

BIND( CONCAT(CONCAT(STR(?class),"-"),STR(?code)) AS ?FullID )
BIND(IF(?class = "AME", "Administration Method",
IF(?class = "BDF", "Basic Dose Form",
IF(?class = "CDF", "Combined Pharmaceutical Dose Form",
IF(?class = "CLO", "Closure",
IF(?class = "CMP", "Combined Pack",
IF(?class = "CMT", "Combined Term",
IF(?class = "CON", "Container",
IF(?class = "DEV", "Administration Device",
IF(?class = "FIL", "FIL",
IF(?class = "ISI", "Intended Site",
IF(?class = "MAP", "MAP",
IF(?class = "PAC", "Packaging",
IF(?class = "PDF", "Pharmaceutical Dose Form",
IF(?class = "PFT", "Patient Friendly Term",
IF(?class = "RCA", "Release Characteristics",
IF(?class = "ROA", "Route of Administration",
IF(?class = "SOM", "State of Matter",
IF(?class = "TRA", "Transformation",
IF(?class = "UOP", "Unit of Presentation",
?unbound))))))))))))))))))) AS ?Class_Text)

BIND(IF(?class = "AME", idmp-phdf:AdministrationMethod,
IF(?class = "BDF", idmp-phdf:BasicDoseForm,
IF(?class = "CDF", idmp-mprd:CombinedPharmaceuticalDoseForm,
IF(?class = "CLO", idmp-phdf:Closure,
IF(?class = "CMP", idmp-phdf:CombinedPack,
IF(?class = "CMT", idmp-phdf:CombinedTerm,
IF(?class = "CON", idmp-mprd:Container,
IF(?class = "DEV", idmp-phdf:AdministrationDevice,
IF(?class = "FIL", "FIL",
IF(?class = "ISI", idmp-phdf:IntendedSite,
IF(?class = "MAP", "MAP",
IF(?class = "PAC", idmp-phdf:Packaging,
IF(?class = "PDF", idmp-mprd:PharmaceuticalDoseForm,
IF(?class = "PFT", idmp-phdf:PatientFriendlyTerm,
IF(?class = "RCA", idmp-phdf:ReleaseCharacteristics,
IF(?class = "ROA", idmp-phdf:RouteOfAdministration,
IF(?class = "SOM", idmp-phdf:StateOfMatter,
IF(?class = "TRA", idmp-phdf:Transformation,
IF(?class = "UOP", idmp-uom:UnitOfPresentation,
?unbound))))))))))))))))))) AS ?IDMP_Class)

BIND(CONCAT("EDQM ", ?Class_Text) AS ?IntroText)

BIND(
    IF(?domain = "Human only", <https://spor.ema.europa.eu/v1/lists/100000000004/terms/100000000012>,
    IF(?domain = "Veterinary only", <https://spor.ema.europa.eu/v1/lists/100000000004/terms/100000000013>,
    IF(?domain = "Human and Veterinary", <https://spor.ema.europa.eu/v1/lists/100000000004/terms/100000000014>,
    ?unbound )))
    AS ?IdmpSubstanceDomain )

BIND("en" AS ?english )

BIND( STRLANG("{?term}","{?english}") AS ?Name_english )

BIND(IF(?definition,STRLANG("{?definition}","{?english}"),?unbound) AS ?Definition_english)

BIND(?creation_date AS ?Created_Date)

BIND(?modification_date AS ?Last_updated)

BIND(IF(?comment,?comment,?unbound) AS ?comment_final)

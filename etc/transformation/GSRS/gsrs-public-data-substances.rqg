PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX idmp: <https://purl.iso.org/idmp/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX idmp-sub:  <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>
PREFIX idmp-ra:  <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/>
PREFIX cmns-txt:  <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX cmns-dsg: <https://www.omg.org/spec/Commons/Designators/>
PREFIX idmp-x: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ExtensionTBD/>

GENERATE {

    # TEMP FIXES to be included in ontology development
    idmp-ra:EPACompTox rdfs:label "EPA CompTox" .
    idmp-sub:hasInhibitorTarget rdfs:label "has inhibitor target" .

    # Ontology
    <https://data.pistoiaalliance.org/idmp/gsrs> a owl:Ontology ;
        rdfs:label "GSRS Public Data" ;
        owl:imports <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/> ;
    .

    # Substance
    ?Substance a ?SubstanceType ;
        ?hasMolecularFormulaByMoietyProperty ?structureFormula ;
        cmns-txt:hasTextValue ?approvalID ;
        idmp-sub:hasDefiningMolecularFormula ?structureFormula ;
        idmp-sub:hasDefiningMolecularWeight ?structureMolecularWeight ;
        idmp-sub:hasDefiningStereochemistry ?Stereochemistry ;
        idmp-sub:hasDefiningStructure ?MolecularStructure ;
    .

    # Molecular Structure
    ?MolecularStructure a idmp-sub:MolecularStructure ;
        idmp-sub:hasSMILESValue ?structureSmilesString ;
        cmns-dsg:isDescribedBy ?StructureMolfileURI;
    .

    # Molecular Structure Molfile
    ?StructureMolfileURI a idmp-sub:Molfile;
        idmp-sub:hasStructuralRepresentationText ?structureMolfile;
    .


    GENERATE{
        ?Substance idmp-sub:hasMoiety ?Moiety;
        .

        ?Moiety a idmp-sub:Moiety;
            rdfs:label ?moietyFormula;
            idmp-sub:hasDefiningStructure ?MoietyStructure;
            idmp-sub:hasDefiningMolecularWeight ?MoietyStructureMolecularWeight ;
            idmp-sub:hasDefiningMolecularFormula ?moietyFormula ;
            idmp-sub:hasDefiningStereochemistry ?MoietyStereochemistry ;
        .

        ?MoietyStructure a idmp-sub:MolecularStructure ;
            idmp-sub:hasSMILESValue ?MoietyStructureSmilesString ;
            cmns-dsg:isDescribedBy ?MoietyStructureMolfileURI;
        .

        ?MoietyStructureMolfileURI a idmp-sub:Molfile;
            idmp-sub:hasStructuralRepresentationText ?MoietyStructureMolfile;
        .
    }
    # Molecular Structure Moieties
    # This part about moieties needs rework! (not a problem for current transform)
    ITERATOR iter:JSONSurfer(?moieties,
        "$[*]",
        "$.hash",
        "$.formula",
        "$.molfile",
        "$.smiles",
        "$.stereochemistry",
        "$.mwt",
        "$.uuid"
    ) AS
    ?moiety
    ?moietyHash
    ?moietyFormula
    ?MoietyStructureMolfile
    ?MoietyStructureSmilesString
    ?MoietystructureStereochemistry
    ?MoietyStructureMolecularWeight
    ?moietyStructureID

    BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }/moieties({ ?moietyStructureID })> AS ?Moiety )
    BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }/moieties({ ?moietyStructureID })/structure> AS ?MoietyStructure )
    BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }/moieties({ ?moietyStructureID })/structure/molfile> AS ?MoietyStructureMolfileURI )

    BIND(IF(str(?MoietystructureStereochemistry) = "ABSOLUTE", idmp-sub:Stereochemistry-Absolute ,
    IF(str(?MoietystructureStereochemistry) = "ACHIRAL", idmp-sub:Stereochemistry-Achiral ,
    IF(str(?MoietystructureStereochemistry) = "AXIAL", idmp-sub:Stereochemistry-Axial ,
    IF(str(?MoietystructureStereochemistry) = "AXIAL R", idmp-sub:Stereochemistry-AxialR ,
    IF(str(?MoietystructureStereochemistry) = "AXIAL S", idmp-sub:Stereochemistry-AxialS ,
    IF(str(?MoietystructureStereochemistry) = "CHIRAL", idmp-sub:Stereochemistry-Chiral ,
    IF(str(?MoietystructureStereochemistry) = "CIS", idmp-sub:Stereochemistry-Cis ,
    IF(str(?MoietystructureStereochemistry) = "EPIMERIC", idmp-sub:Stereochemistry-Epimeric ,
    IF(str(?MoietystructureStereochemistry) = "MESO", idmp-sub:Stereochemistry-Meso ,
    IF(str(?MoietystructureStereochemistry) = "MIXED", idmp-sub:Stereochemistry-Mixed ,
    IF(str(?MoietystructureStereochemistry) = "OCTAHEDRAL 12", idmp-sub:Stereochemistry-Octahedral12 ,
    IF(str(?MoietystructureStereochemistry) = "OCTAHEDRAL 22", idmp-sub:Stereochemistry-Octahedral22 ,
    IF(str(?MoietystructureStereochemistry) = "OCTAHEDRAL 21", idmp-sub:Stereochemistry-Octahedral21 ,
    IF(str(?MoietystructureStereochemistry) = "RACEMIC", idmp-sub:Stereochemistry-Racemic ,
    IF(str(?MoietystructureStereochemistry) = "SQUARE PLANAR 1", idmp-sub:Stereochemistry-SquarePlanar1 ,
    IF(str(?MoietystructureStereochemistry) = "SQUARE PLANAR 2", idmp-sub:Stereochemistry-SquarePlanar2 ,
    IF(str(?MoietystructureStereochemistry) = "SQUARE PLANAR 3", idmp-sub:Stereochemistry-SquarePlanar3 ,
    IF(str(?MoietystructureStereochemistry) = "SQUARE PLANAR 4", idmp-sub:Stereochemistry-SquarePlanar4 ,
    IF(str(?MoietystructureStereochemistry) = "TETRAHEDRAL", idmp-sub:Stereochemistry-Tetrahedral ,
    IF(str(?MoietystructureStereochemistry) = "TRANS", idmp-sub:Stereochemistry-Trans ,
    IF(str(?MoietystructureStereochemistry) = "UNKNOWN", idmp-sub:Stereochemistry-Unknown ,
    ?MoietystructureStereochemistry )))))))))))))))))))))
    AS ?MoietyStereochemistry)
    .

    GENERATE{
        # Relationships
        ?Substance ?SubstancesRelationshipProperty ?RelatedSubstance;
        .
        # Is Active Moiety Of
        ?RelatedSubstance ?IsActiveMoietyOfProperty ?Substance;
        .
    }
    ITERATOR iter:JSONSurfer(?relationships,
        "$[*]",
        "$.type",
        "$.relatedSubstance.approvalID"
    ) AS
    ?Relationship
    ?RelationshipType
    ?RelatedSubstanceApprovalID

    BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?RelatedSubstanceApprovalID }> AS ?RelatedSubstance)

    BIND(
        IF(str(?RelationshipType) = "ACTIVE MOIETY" || str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", idmp-sub:hasActiveMoiety,
        ?nothing)
        AS ?SubstancesRelationshipProperty)

    BIND(
        IF(str(?RelationshipType) = "ACTIVE MOIETY" || str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", idmp-sub:hasActiveMoiety,
        IF(str(?RelationshipType) = "INHIBITOR->TARGET", idmp-sub:hasInhibitorTarget,
        skos:related))
        AS ?SubstancesRelationship)

    BIND(IF(uri(?SubstancesRelationship) = idmp-sub:hasActiveMoiety, idmp-sub:isActiveMoietyOf, ?nothing) AS ?IsActiveMoietyOfProperty)
    .

}
# General Substance Info + Molecular Structure
ITERATOR iter:JSONSurfer(<public_data.json>,
    "$[*]",
    "$.approvalID",
    "$.substanceClass",
    "$.structure.id",
    "$.structure.smiles",
    "$.structure.formula",
    "$.structure.stereochemistry",
    "$.structure.mwt",
    "$.structure.molfile",
    "$.moieties",
    "$.relationships"
    ) AS

?I1
?approvalID
?substanceClass
?structureID
?structureSmilesString
?structureFormula
?structureStereochemistry
?structureMolecularWeight
?structureMolfile
?moieties
?relationships

BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }> AS ?Substance )
BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }/structure> AS ?MolecularStructure )
BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }/structure/molfile> AS ?StructureMolfileURI )

BIND(IF(contains(str(?structureFormula), "."), idmp-sub:hasDefiningMolecularFormulaByMoiety , ?nothing ) AS ?hasMolecularFormulaByMoietyProperty)

BIND(IF(str(?substanceClass) = "chemical", idmp-sub:ChemicalSubstance ,
    IF(str(?substanceClass) = "protein", idmp-sub:ProteinSubstance ,
    IF(str(?substanceClass) = "nucleicAcid", idmp-sub:NucleicAcidSubstance ,
    IF(str(?substanceClass) = "mixture", idmp-sub:Mixture ,
    IF(str(?substanceClass) = "polymer", idmp-sub:PolymerSubstance ,
    IF(str(?substanceClass) = "structurallyDiverse", idmp-sub:StructurallyDiverseSubstance ,
    IF(str(?substanceClass) = "specifiedSubstance", idmp-sub:SpecifiedSubstance ,
    idmp-sub:Substance )))))))
    AS ?SubstanceType)

BIND(IF(str(?structureStereochemistry) = "ABSOLUTE", idmp-sub:Stereochemistry-Absolute ,
    IF(str(?structureStereochemistry) = "ACHIRAL", idmp-sub:Stereochemistry-Achiral ,
    IF(str(?structureStereochemistry) = "AXIAL", idmp-sub:Stereochemistry-Axial ,
    IF(str(?structureStereochemistry) = "AXIAL R", idmp-sub:Stereochemistry-AxialR ,
    IF(str(?structureStereochemistry) = "AXIAL S", idmp-sub:Stereochemistry-AxialS ,
    IF(str(?structureStereochemistry) = "CHIRAL", idmp-sub:Stereochemistry-Chiral ,
    IF(str(?structureStereochemistry) = "CIS", idmp-sub:Stereochemistry-Cis ,
    IF(str(?structureStereochemistry) = "EPIMERIC", idmp-sub:Stereochemistry-Epimeric ,
    IF(str(?structureStereochemistry) = "MESO", idmp-sub:Stereochemistry-Meso ,
    IF(str(?structureStereochemistry) = "MIXED", idmp-sub:Stereochemistry-Mixed ,
    IF(str(?structureStereochemistry) = "OCTAHEDRAL 12", idmp-sub:Stereochemistry-Octahedral12 ,
    IF(str(?structureStereochemistry) = "OCTAHEDRAL 22", idmp-sub:Stereochemistry-Octahedral22 ,
    IF(str(?structureStereochemistry) = "OCTAHEDRAL 21", idmp-sub:Stereochemistry-Octahedral21 ,
    IF(str(?structureStereochemistry) = "RACEMIC", idmp-sub:Stereochemistry-Racemic ,
    IF(str(?structureStereochemistry) = "SQUARE PLANAR 1", idmp-sub:Stereochemistry-SquarePlanar1 ,
    IF(str(?structureStereochemistry) = "SQUARE PLANAR 2", idmp-sub:Stereochemistry-SquarePlanar2 ,
    IF(str(?structureStereochemistry) = "SQUARE PLANAR 3", idmp-sub:Stereochemistry-SquarePlanar3 ,
    IF(str(?structureStereochemistry) = "SQUARE PLANAR 4", idmp-sub:Stereochemistry-SquarePlanar4 ,
    IF(str(?structureStereochemistry) = "TETRAHEDRAL", idmp-sub:Stereochemistry-Tetrahedral ,
    IF(str(?structureStereochemistry) = "TRANS", idmp-sub:Stereochemistry-Trans ,
    IF(str(?structureStereochemistry) = "UNKNOWN", idmp-sub:Stereochemistry-Unknown ,
    ?structureStereochemistry )))))))))))))))))))))
    AS ?Stereochemistry)

PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX cmns-cxtdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
PREFIX cmns-rlcmp: <https://www.omg.org/spec/Commons/RolesAndCompositions/>
PREFIX cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>
PREFIX idmp-ra:  <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/>
PREFIX idmp-nara: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/NorthAmericanJurisdiction/NorthAmericanRegistrationAuthorities/>
PREFIX idmp-narga: <https://spec.pistoiaalliance.org/idmp/ontology/ISO/NorthAmericanJurisdiction/NorthAmericanRegulatoryAgencies/>
PREFIX idmp-sub:  <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>

GENERATE {

    # Relationships
    ?Substance ?SubstancesRelationshipProperty ?RelatedSubstance .

    # Has Active Moiety Role and Concept
    ?RelatedSubstanceAssociatedWithActiveMoietyRole cmns-rlcmp:playsRole ?ActiveMoietyRole .

    # Is Active Moiety Of
    # ?RelatedSubstance ?IsActiveMoietyOfProperty ?Substance .

    ?ActiveMoietyRole a ?MoietyRole ;
        cmns-cxtdsg:isApplicableIn ?RegulatoryContext;
        cmns-rlcmp:isPlayedBy ?RelatedSubstanceAssociatedWithActiveMoietyRole ;
        cmns-rlcmp:isManifestedIn ?SubstanceAssociatedWithActiveMoietyRole ;
        .

    ?SubstanceAssociatedWithActiveMoietyRole cmns-rlcmp:manifests ?ActiveMoietyRole .

    ?RegulatoryContext cmns-cxtdsg:appliesTo ?ActiveMoietyRole .

}

# General Substance Info + Molecular Structure
ITERATOR iter:JSONSurfer(<public_data.json>,
    "$[*]",
    "$.approvalID",
    "$.relationships"
    ) AS

?I1
?approvalID
?relationships

BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }> AS ?Substance )

# Relationships
ITERATOR iter:JSONSurfer(?relationships,
    "$[*]",
    "$.type",
    "$.relatedSubstance.approvalID"
    ) AS

?Relationship
?RelationshipType
?RelatedSubstanceApprovalID

BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?RelatedSubstanceApprovalID }> AS ?RelatedSubstance)

BIND(
    IF(?Substance = ?RelatedSubstance, ?nothing, # avoid meaningless triple
    IF(str(?RelationshipType) = "ACTIVE MOIETY" || str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", ?nothing,
    skos:related))
    AS ?SubstancesRelationshipProperty)

BIND(
    IF(?Substance = ?RelatedSubstance, ?nothing, # avoid meaningless triple
    IF(str(?RelationshipType) = "ACTIVE MOIETY" || str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", idmp-sub:hasActiveMoiety,
    skos:related))
    AS ?SubstancesRelationship)

# HAS ACTIVE MOIETY ROLE AND CONTEXT
BIND(IF(uri(?SubstancesRelationship) = idmp-sub:hasActiveMoiety, ?approvalID, ?nothing) AS ?SourceSubstanceApprovalID)

BIND(
    IF(str(?RelationshipType) = "ACTIVE MOIETY", <https://data.pistoiaalliance.org/role/{ ?SourceSubstanceApprovalID }_MoietyRole_FDA_general_{ ?RelatedSubstanceApprovalID }>,
    IF(str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", <https://data.pistoiaalliance.org/role/{ ?SourceSubstanceApprovalID }_MoietyRole_FDA_granted_exclusivity_{ ?RelatedSubstanceApprovalID }>,
    ?nothing))
    AS ?ActiveMoietyRole)

BIND(IF(uri(?SubstancesRelationship) = idmp-sub:hasActiveMoiety, ?Substance, ?nothing) AS ?SubstanceAssociatedWithActiveMoietyRole)
BIND(IF(uri(?SubstancesRelationship) = idmp-sub:hasActiveMoiety, ?RelatedSubstance, ?nothing) AS ?RelatedSubstanceAssociatedWithActiveMoietyRole)
BIND(IF(uri(?SubstancesRelationship) = idmp-sub:hasActiveMoiety, idmp-sub:ActiveMoietyRole, ?nothing) AS ?MoietyRole)
BIND(IF(uri(?SubstancesRelationship) = idmp-sub:hasActiveMoiety, idmp-sub:isActiveMoietyOf, ?nothing) AS ?IsActiveMoietyOfProperty)

BIND(
    IF(str(?RelationshipType) = "ACTIVE MOIETY", idmp-narga:RegulatoryContext-FoodAndDrugAdministrationGeneral,
    IF(str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", idmp-narga:RegulatoryContext-ActiveMoietyForFoodAndDrugAdministrationExclusivity,
    ?nothing))
    AS ?RegulatoryContext)

PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX cmns-cstdsg: <https://www.omg.org/spec/Commons/ContextualDesignators/>
PREFIX cmns-pts: <https://www.omg.org/spec/Commons/PartiesAndSituations/>
PREFIX idmp-ra:  <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-RegistrationAuthorities/>
PREFIX idmp-sub:  <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>

GENERATE { 

    # Relationships
    ?Substance ?SubstancesRelationshipProperty ?RelatedSubstance .

    # Has Active Moiety Role and Concept
    ?RelatedSubstanceAssociatedWithActiveMoietyRole cmns-pts:playsRole ?ActiveMoietyRole .

    # Is Active Moiety Of
    ?RelatedSubstance ?IsActiveMoietyOfProperty ?Substance .

    ?ActiveMoietyRole a ?MoietyRole ;
        cmns-cstdsg:isApplicableIn ?RegulatoryContext;
        cmns-pts:isPlayedBy ?RelatedSubstanceAssociatedWithActiveMoietyRole ;
        cmns-pts:isRealizedIn ?SubstanceAssociatedWithActiveMoietyRole ;
        .

    ?SubstanceAssociatedWithActiveMoietyRole cmns-pts:realizes ?ActiveMoietyRole .

    ?RegulatoryContext cmns-cstdsg:appliesTo ?ActiveMoietyRole .

}
SOURCE <gsrs-public-data-iteration.json> AS ?source

# General Substance Info + Molecular Structure
ITERATOR iter:JSONPath(?source,
    "$[*]",
    "$.approvalID",
    "$.relationships"
    ) AS

?I1 
?approvalID 
?relationships

BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }> AS ?Substance )

# Relationships
ITERATOR iter:JSONPath(?relationships, 
    "$[*]",
    "$.type",
    "$.relatedSubstance.approvalID"
    ) AS 

?Relationship
?RelationshipType
?RelatedSubstanceApprovalID

BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?RelatedSubstanceApprovalID }> AS ?RelatedSubstance)

BIND(
    IF(str(?RelationshipType) = "ACTIVE MOIETY" || str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", idmp-sub:hasActiveMoiety,
    IF(str(?RelationshipType) = "INHIBITOR->TARGET", idmp-sub:hasInhibitorTarget,
    skos:related))
    AS ?SubstancesRelationshipProperty)

# HAS ACTIVE MOIETY ROLE AND CONTEXT
BIND(IF(uri(?SubstancesRelationshipProperty) = idmp-sub:hasActiveMoiety, ?approvalID, ?nothing) AS ?SourceSubstanceApprovalID)

BIND(
    IF(str(?RelationshipType) = "ACTIVE MOIETY", <https://data.pistoiaalliance.org/idmp/gsrs/role/{ ?RelatedSubstanceApprovalID }_ActiveMoietyRole_FDA_general_{ ?SourceSubstanceApprovalID }>,
    IF(str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", <https://data.pistoiaalliance.org/idmp/gsrs/role/{ ?RelatedSubstanceApprovalID }_ActiveMoietyRole_FDA_patent_exclusive_{ ?SourceSubstanceApprovalID }>,
    ?nothing))
    AS ?ActiveMoietyRole)

BIND(IF(uri(?SubstancesRelationshipProperty) = idmp-sub:hasActiveMoiety, ?Substance, ?nothing) AS ?SubstanceAssociatedWithActiveMoietyRole)
BIND(IF(uri(?SubstancesRelationshipProperty) = idmp-sub:hasActiveMoiety, ?RelatedSubstance, ?nothing) AS ?RelatedSubstanceAssociatedWithActiveMoietyRole)
BIND(IF(uri(?SubstancesRelationshipProperty) = idmp-sub:hasActiveMoiety, idmp-sub:ActiveMoietyRole, ?nothing) AS ?MoietyRole)
BIND(IF(uri(?SubstancesRelationshipProperty) = idmp-sub:hasActiveMoiety, idmp-sub:isActiveMoietyOf, ?nothing) AS ?IsActiveMoietyOfProperty)

BIND(
    IF(str(?RelationshipType) = "ACTIVE MOIETY", idmp-ra:RegulatoryContext-FoodAndDrugAdministrationGeneral,
    IF(str(?RelationshipType) = "ACTIVE MOIETY (FOR EXCLUSIVITY)", idmp-ra:RegulatoryContext-FoodAndDrugAdministrationPatentExclusive,
    ?nothing))
    AS ?RegulatoryContext)
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX cmns-dsg:  <https://www.omg.org/spec/Commons/Designators/>
PREFIX cmns-txt:  <https://www.omg.org/spec/Commons/TextDatatype/>
PREFIX idmp-sub:  <https://spec.pistoiaalliance.org/idmp/ontology/ISO/ISO11238-Substances/>

GENERATE { 

    # Substance
    ?Substance ?labelProperty ?OfficialSubstanceName ;
        rdfs:label ?OfficialSubstanceName ;
        cmns-dsg:hasName ?SubstanceName ;
    .

    # Names
    ?SubstanceName a cmns-dsg:ContextualName ;
        cmns-txt:hasTextValue ?SubstanceNameValue ;
        cmns-dsg:isNameOf ?Substance ;
        idmp-sub:hasSubstanceNameType ?SubstanceNameType ;
        .
}
SOURCE <public_data.json> AS ?source

ITERATOR iter:JSONSurfer(?source,
    "$[*]",
    "$.approvalID",
    "$.names"
    ) AS

?I1 
?approvalID 
?names

BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }> AS ?Substance )

# Names 
ITERATOR iter:JSONSurfer(?names, 
    "$[*]",
    "$.uuid",
    "$.name",
    "$.type",
    "$.displayName"
    ) AS

?I1_Names
?NameUuid
?NameName
?NameType
?NameDisplayName

BIND(<https://gsrs.ncats.nih.gov/api/v1/substances/{ ?approvalID }/{?NameUuid}> AS ?SubstanceName)
BIND(?NameName AS ?SubstanceNameValue)
BIND(IF(str(?NameDisplayName) = "true", ?NameName, ?nothing) AS ?OfficialSubstanceName)
BIND(IF(str(?NameDisplayName) = "true", skos:prefLabel, ?nothing) AS ?labelProperty)

BIND(
    IF(str(?NameType) = "bn", idmp-sub:SubstanceNameClassifier-BrandName , 
    IF(str(?NameType) = "cd", idmp-sub:SubstanceNameClassifier-CompanyCode ,
    IF(str(?NameType) = "cn", idmp-sub:SubstanceNameClassifier-CommonName ,
    IF(str(?NameType) = "of", idmp-sub:SubstanceNameClassifier-OfficialName ,
    IF(str(?NameType) = "sys", idmp-sub:SubstanceNameClassifier-SystematicName , 
    idmp-sub:SubstanceNameClassifier )))))
    AS ?SubstanceNameType)
